plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'info.jackwarren'
version '1.0.0'
sourceCompatibility = 1.8
mainClassName = 'MainKt'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.github.ajalt:clikt:2.4.0'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-json:$ktor_version"
    implementation "io.ktor:ktor-client-gson:$ktor_version"
    testImplementation "io.ktor:ktor-client-mock-jvm:$ktor_version"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
}

Closure<Void> kotlinCompilationOptions = {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xuse-experimental=" + [
                "io.ktor.util.KtorExperimentalAPI"
        ].join(",")
    }
}

compileKotlin(kotlinCompilationOptions)
compileTestKotlin(kotlinCompilationOptions)

// Special jar type that includes all necessary dependencies
shadowJar {
    archiveClassifier.set ''
    archiveVersion.set ''
}

task copyJar {
    dependsOn ":shadowJar"
    doLast {
        copy {
            from "build/libs/${project.name}.jar"
            into "./"
        }
    }
}

test {
    useJUnitPlatform()
}